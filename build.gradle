plugins {
    id 'application'
    id 'java-library'
}

group = 'tum-gis'
version = '2.0.0'
description = 'A change detection tool for CityGML datasets using the graph database Neo4j.'

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(11))
    }
}

repositories {
    maven {
        url "https://repo.maven.apache.org/maven2"
        artifactUrls "https://repo.osgeo.org/repository/release"
    }
    maven {
        url "https://raw.github.com/neo4j-contrib/m2/master/releases"
    }
    maven {
        url "https://raw.github.com/neo4j-contrib/m2/master/snapshots"
    }
    maven {
        url "https://repo.osgeo.org/repository/release"
    }
    mavenCentral()
}

dependencies {
    implementation 'org.citygml4j:citygml4j:2.12.1'
    implementation 'org.json:json:20220320'
    implementation 'org.jsonschema2pojo:jsonschema2pojo-core:1.1.2'
    implementation 'com.google.code.gson:gson:2.9.0'
    implementation 'org.neo4j:neo4j-dbms:4.4.8'
    implementation 'org.neo4j.driver:neo4j-java-driver:4.4.6'
    implementation 'org.neo4j:neo4j:4.4.8'
    implementation 'org.neo4j:neo4j-spatial:0.28.1-neo4j-4.4.3'
    implementation 'org.neo4j:neo4j-bolt:4.4.8'
    implementation 'ch.qos.logback:logback-classic:1.2.11'
}

configurations.all {
    exclude(group: 'org.slf4j', module: 'slf4j-nop')
}

run {
    main('components.Controller')
    systemProperty('logback.configurationFile', 'src/main/java/components/logger/logback.xml')
}

// Run this task to rebuild the mapping rule files citygml.json, gml.json and xal.json
task createMappingRules(type: JavaExec) {
    group('utils')
    description('Build mapping rules in JSON files')
    classpath(sourceSets.main.runtimeClasspath)
    main('utils.MappingRulesUtils')
}

// Run this task to rebuild the config Java classes in case of changed conf.json
task createConfClasses(type: JavaExec) {
    group('utils')
    description('Build config Java classes based on the structure of config JSON files')
    classpath(sourceSets.main.runtimeClasspath)
    main('utils.ConfUtils')
}