plugins {
    id 'java'
    id 'maven-publish'
    id 'com.jfrog.artifactory' version '4.21.0'
}

group = 'tum-gis'
version = '2.0.0'
description = 'A change detection tool for CityGML datasets using the graph database Neo4j.'

java {
    sourceCompatibility = JavaVersion.VERSION_11
    //withJavadocJar()
    withSourcesJar()
}

repositories {
    maven {
        url "https://repo.maven.apache.org/maven2"
        artifactUrls "https://repo.osgeo.org/repository/release"
    }
    maven {
        url "https://raw.github.com/neo4j-contrib/m2/master/releases"
    }
    maven {
        url "https://raw.github.com/neo4j-contrib/m2/master/snapshots"
    }
    maven {
        url "https://repo.osgeo.org/repository/release"
    }
    mavenCentral()
}

dependencies {
    implementation "org.citygml4j:citygml4j:2.12.1"
    implementation 'org.json:json:20220320'
    implementation 'org.neo4j:neo4j-dbms:4.4.8'
    implementation "org.neo4j.driver:neo4j-java-driver:4.4.6"
    implementation "org.neo4j:neo4j:4.4.8"
    //implementation 'org.apache.httpcomponents:httpclient:4.5.13'
    implementation "org.neo4j:neo4j-spatial:0.28.1-neo4j-4.4.3"
    implementation 'org.neo4j:neo4j-bolt:4.4.8'
    implementation 'ch.qos.logback:logback-classic:1.2.11'
    implementation 'org.jsonschema2pojo:jsonschema2pojo-core:1.1.2'
    implementation 'com.google.code.gson:gson:2.9.0'
    testImplementation 'junit:junit:4.13.2'
}

configurations.all {
    exclude module: 'slf4j-nop'
}

// https://github.com/gradle/gradle/issues/15538
//tasks.withType( JavaCompile ).configureEach {
//    options.forkOptions.jvmArgs.addAll( ['--add-opens', 'jdk.compiler/com.sun.tools.javac.code=ALL-UNNAMED'] )
//}

javadoc {
    options.addStringOption('Xdoclint:none', '-quiet')
}

jar {
    manifest {
        attributes('Implementation-Title': project.name,
                'Implementation-Version': project.version
        )
    }
    into('META-INF') {
        from 'LICENSE'
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            pom {
                name = project.name
                description = project.description
                url = 'https://github.com/tum-gis'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution = 'repo'
                    }
                }
                developers {
                    developer {
                        id = 'son-hnguyen'
                        name = 'Son H. Nguyen'
                        email = 'son.nguyen@tum.de'
                        roles = ['developer']
                        timezone = '+1'
                    }
                }
            }
        }
    }
}

artifactory {
    contextUrl = project.hasProperty('artifactory_contextUrl') ? project.property('artifactory_contextUrl') : System.getenv('artifactory_contextUrl')
    publish {
        repository {
            repoKey = 'maven'
            username = project.hasProperty('artifactory_user') ? project.property('artifactory_user') : System.getenv('artifactory_user')
            password = project.hasProperty('artifactory_password') ? project.property('artifactory_password') : System.getenv('artifactory_password')
            maven = true
        }
        defaults {
            publications('mavenJava')
        }
    }
}