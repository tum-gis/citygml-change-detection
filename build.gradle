import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java'
    id 'maven-publish'
    id 'com.jfrog.bintray' version '1.8.4'
}

group = 'tum-gis'
version = '0.1.0'
description = 'A change detection tool for CityGML datasets using the graph database Neo4j.'

sourceCompatibility = '1.8'

ext {
    date = new Date()
}

repositories {
    mavenLocal()
    maven {
        url = uri('https://raw.github.com/neo4j-contrib/m2/master/releases')
    }
    maven {
        url = uri('https://raw.github.com/neo4j-contrib/m2/master/snapshots')
    }
    maven {
        url = uri('https://repo.maven.apache.org/maven2')
    }
    jcenter()
}

dependencies {
    implementation 'org.citygml4j:citygml4j:2.4.4'
    implementation 'org.neo4j.driver:neo4j-java-driver:1.2.1'
    implementation 'org.neo4j:neo4j:3.2.5'
    implementation 'org.apache.httpcomponents:httpclient:4.5.2'
    implementation 'com.graphaware.neo4j:tx-executor:3.1.0.44'
    implementation 'org.neo4j:neo4j-spatial:0.24.1-neo4j-3.2.5'
    testImplementation 'junit:junit:4.12'
}

jar {
    manifest {
        attributes('Implementation-Title': project.name,
                'Implementation-Version': project.version
        )
    }
    into('META-INF') {
        from 'LICENSE'
        from('resources/doc/README.txt') {
            filter(ReplaceTokens, tokens: [
                    version  : project.version,
                    date     : date.format('yyyy-MM-dd'),
                    copyright: date.format('yyyy')
            ])
        }
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

tasks.withType(Javadoc).all { enabled = false }

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            pom {
                name = project.name
                description = project.description
                url = 'https://github.com/tum-gis'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution = 'repo'
                    }
                }
                developers {
                    developer {
                        id = 'son-hnguyen'
                        name = 'Son H. Nguyen'
                        email = 'son.nguyen@tum.de'
                        timezone = '+1'
                    }
                }
            }
        }
    }
}

bintray {
    user = System.getProperty('bintray.user')
    key = System.getProperty('bintray.key')

    publications = ['mavenJava']
    pkg {
        repo = 'maven'
        userOrg = 'tum-gis'
        name = project.name
        desc = project.description
        websiteUrl = 'https://github.com/tum-gis'
        vcsUrl = 'https://github.com/tum-gis/citygml-change-detection.git'
        issueTrackerUrl = 'https://github.com/tum-gis/citygml-change-detection/issues'
        licenses = ['Apache-2.0']
        version {
            name = project.version
        }
    }
}